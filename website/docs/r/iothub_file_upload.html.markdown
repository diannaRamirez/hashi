---
subcategory: "IoT Hub"
layout: "azurerm"
page_title: "Azure Resource Manager: azurerm_iothub_file_upload"
description: |-
  Manages a IotHub File Upload.
---

# azurerm_iothub_file_upload

Manages a IotHub File Upload.

~> **NOTE:** File Upload can be defined either directly on the `azurerm_iothub` resource, or using the `azurerm_iothub_file_upload` resource - but the two cannot be used together. If both are used against the same IoTHub, spurious changes will occur.

~> **Note:** Since this resource is provisioned by default, the Azure Provider will not check for the presence of an existing resource prior to attempting to create it.

## Example Usage

```hcl
resource "azurerm_resource_group" "test" {
  name     = "example-resources"
  location = "West Europe"
}

resource "azurerm_storage_account" "test" {
  name                     = "examplestorage"
  resource_group_name      = azurerm_resource_group.test.name
  location                 = azurerm_resource_group.test.location
  account_tier             = "Standard"
  account_replication_type = "LRS"
}

resource "azurerm_storage_container" "test" {
  name                  = "examplecontainer"
  storage_account_name  = azurerm_storage_account.test.name
  container_access_type = "private"
}

resource "azurerm_iothub" "test" {
  name                = "example-iothub"
  resource_group_name = azurerm_resource_group.test.name
  location            = azurerm_resource_group.test.location

  sku {
    name     = "S1"
    capacity = "1"
  }

  identity {
    type = "SystemAssigned"
  }
}

resource "azurerm_role_assignment" "test_storage_blob_data_contrib" {
  role_definition_name = "Storage Blob Data Contributor"
  scope                = azurerm_storage_account.test.id
  principal_id         = azurerm_iothub.test.identity[0].principal_id
}

resource "azurerm_iothub_file_upload" "test" {
  iothub_id         = azurerm_iothub.test.id
  container_name    = azurerm_storage_container.test.name
  connection_string = azurerm_storage_account.test.primary_blob_connection_string

  authentication_type = "identityBased"

  depends_on = [
    azurerm_role_assignment.test_storage_blob_data_contrib,
  ]
}
```

## Arguments Reference

The following arguments are supported:

* `connection_string` - (Required) The connection string for the Azure Storage account to which files are uploaded.

* `container_name` - (Required) The name of the root container where you upload files. The container need not exist but should be creatable using the connection_string specified.

* `iothub_id` - (Required) The ID of the IoTHub. Changing this forces a new IotHub File Upload to be created.

---

* `authentication_type` - (Optional) Type used to authenticate against the storage account. Possible values are `keyBased` and `identityBased`. Defaults to `keyBased`.

* `default_ttl` - (Optional) The period of time for which a file upload notification message is available to consume before it is expired by the IoT hub, specified as an [ISO 8601 timespan duration](https://en.wikipedia.org/wiki/ISO_8601#Durations). This value must be between 1 minute and 48 hours, and evaluates to `PT1H` by default.

* `identity_id` - (Optional) ID of the User Managed Identity used to authenticate against the storage account.

-> **NOTE:** `identity_id` can only be specified when `authentication_type` is `identityBased`. It must be one of the `identity_ids` of the Iot Hub. If not specified when `authentication_type` is `identityBased`, System Assigned Managed Identity of the Iot Hub will be used.

* `lock_duration` - (Optional) The lock duration for the file upload notifications queue, specified as an [ISO 8601 timespan duration](https://en.wikipedia.org/wiki/ISO_8601#Durations). This value must be between 5 and 300 seconds, and evaluates to `PT1M` by default.

* `max_delivery_count` - (Optional) The number of times the IoT hub attempts to deliver a file upload notification message. It evaluates to `10` by default.

* `notifications` - (Optional) Used to specify whether file notifications are sent to IoT Hub on upload. It evaluates to false by default.

* `sas_ttl` - (Optional) The period of time for which the SAS URI generated by IoT Hub for file upload is valid, specified as an [ISO 8601 timespan duration](https://en.wikipedia.org/wiki/ISO_8601#Durations). This value must be between 1 minute and 24 hours, and evaluates to `PT1H` by default.

## Attributes Reference

In addition to the Arguments listed above - the following Attributes are exported: 

* `id` - The ID of the IotHub File Upload.

## Timeouts

The `timeouts` block allows you to specify [timeouts](https://www.terraform.io/docs/configuration/resources.html#timeouts) for certain actions:

* `create` - (Defaults to 30 minutes) Used when creating the IotHub File Upload.
* `update` - (Defaults to 30 minutes) Used when updating the IotHub File Upload.
* `read` - (Defaults to 5 minutes) Used when retrieving the IotHub File Upload.
* `delete` - (Defaults to 30 minutes) Used when deleting the IotHub File Upload.

## Import

IotHub File Uploads can be imported using the `resource id`, e.g.

```shell
terraform import azurerm_iothub_file_upload.file_upload1 /subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/mygroup1/providers/Microsoft.Devices/IotHubs/hub1/FileUpload/default
```
~> **NOTE:** As there may only be a single fallback route per IoTHub, the id always ends with `/FileUpload/default`.
